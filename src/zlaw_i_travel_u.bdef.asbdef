// This is where you will have your SAVE SEQUENCE METHODS (Saver Class)
// NOTE: kapag ito lang dineclare mong class.. both Handler Class and Saver Class mapupunta sa isang BP (Behavior pool)...
//       we don't want that.. best practice is pag hiwa-hiwalayin ang mga classes for flexibility... :)
unmanaged implementation in class zbp_law_i_travel_u unique;
strict ( 2 );

define behavior for ZLAW_I_Travel_U alias Travel
implementation in class zbp_travel_law_u unique // This is where you will have your Handler Class

/* Early Numbering: assignment of primary key values at the beginning of the interaction phase
 Late Numbering: assigns primary key values just before the data is saved to the database */
late numbering

lock master
authorization master ( global )
etag master LastChangedAt
{
  create;
  update;
  delete;

  field ( readonly ) TravelID, LastChangedAt;
  field ( mandatory ) AgencyID, CustomerID, BeginDate, EndDate;

  association _Booking { create ( features : instance ); }

  mapping for /dmo/travel control /dmo/s_travel_intx // this is your %control structure during implementation
    {
      AgencyID      = agency_id;
      BeginDate     = begin_date;
      BookingFee    = booking_fee;
      CurrencyCode  = currency_code;
      CustomerID    = customer_id;
      EndDate       = end_date;
      LastChangedAt = lastchangedat;
      Memo          = description;
      Status        = status;
      TotalPrice    = total_price;
      TravelID      = travel_id;
    }
}

define behavior for ZLAW_I_Booking_U alias Booking
implementation in class zbp_booking_law_u unique
late numbering
lock dependent by _Travel
authorization dependent by _Travel
etag dependent by _Travel
{
  update;
  delete;

  field ( readonly ) TravelID, BookingID;
  field ( mandatory ) BookingDate, CustomerID, AirlineID, ConnectionID, FlightDate;

  association _Travel;

  mapping for /dmo/booking control /dmo/s_booking_intx
    {
      AirlineID    = carrier_id;
      BookingDate  = booking_date;
      BookingID    = booking_id;
      ConnectionID = connection_id;
      CurrencyCode = currency_code;
      CustomerID   = customer_id;
      FlightDate   = flight_date;
      FlightPrice  = flight_price;
      TravelID     = travel_id;
    }
}