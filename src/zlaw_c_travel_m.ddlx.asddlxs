// Some annotations are not allowed here like ObjectModel
// UI Level Annotations lang pwede dito
@Metadata.layer: #CORE
@Search.searchable: true
@UI.headerInfo: {
    typeName: 'Travel',
    typeNamePlural: 'Travels',
    title: {
        type: #STANDARD,
        label: 'Travel',
        value: 'AgencyName'
    },
    description.value: 'TravelId'
}
annotate entity ZLAW_C_Travel_M with
{
  // Facets
  @UI.facet: [{
      id: 'GeneralInformation',
      purpose: #STANDARD,
      position: 1,
      label: 'General Information',
      type: #IDENTIFICATION_REFERENCE
  },{ 
      id: 'BookingInformation',
      purpose: #STANDARD,
      position: 2,
      label: 'Bookings',
      type: #LINEITEM_REFERENCE,
      targetElement: '_Booking'
  }]


  @UI:{
    lineItem: [
        { position: 1 },
        // Add your Action here.. if you want it in the main page
        { 
            type: #FOR_ACTION,
            dataAction: 'copyTravel',
            label: 'Copy Travel' 
        } 
    ],
    identification: [{ position: 1 }]
  }
  @Search.defaultSearchElement: true
  TravelId;
  @UI:{
    lineItem: [{ position: 2 }],
    selectionField: [{ position: 1 }],
    identification: [{ position: 2 }]
  }
  @Search.defaultSearchElement: true
  @Consumption.valueHelpDefinition: [{
  entity : {
    name: '/DMO/I_Agency_StdVH',
    element: 'AgencyID'
  }}]
  AgencyId;
  @UI:{
    lineItem: [{ position: 3 }],
    selectionField: [{ position: 2 }],
    identification: [{ position: 3 }]
  }
  @Search.defaultSearchElement: true
  @Consumption.valueHelpDefinition: [{
  entity : {
    name: '/DMO/I_Customer_StdVH',
    element: 'CustomerID'
  }}]
  CustomerId;
  @UI:{
    lineItem: [{ position: 4 }],
    identification: [{ position: 4 }]
  }
  BeginDate;
  @UI:{
    lineItem: [{ position: 5 }],
    identification: [{ position: 5 }]
  }
  EndDate;
  //  BookingFee;
  @UI:{
    lineItem: [{ position: 6 }],
    identification: [{ position: 6 }]
  }
  TotalPrice;
  @Consumption.valueHelpDefinition: [{
  entity : {
   name: 'I_CurrencyStdVH',
   element: 'Currency'
  }}]
  CurrencyCode;
  //  Description;
  @UI:{
    lineItem: [{ position: 7 }],
    selectionField: [{ position: 3 }],
    identification: [{ position: 7 }],
    textArrangement: #TEXT_ONLY // Appearance of text in UI
  }
  @Search.defaultSearchElement: true
  @Consumption.valueHelpDefinition: [{
  entity : {
      name: '/DMO/I_Overall_Status_VH',
      element: 'OverallStatus'
  }}]
  OverallStatus;
  @UI.hidden: true
  CreatedBy;
  @UI.hidden: true
  CreatedAt;
  @UI.hidden: true
  LastChangedBy;
  @UI.hidden: true
  LastChangedAt;
  //  AgencyName;
  //  CustomerFirstName;
  //  StatusText;

}